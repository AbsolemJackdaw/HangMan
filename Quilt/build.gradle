import net.darkhax.curseforgegradle.TaskPublishCurseForge

plugins {
    id 'org.quiltmc.loom' version '0.12.+'
    id 'maven-publish'
    id 'net.darkhax.curseforgegradle' version '1.0.11'
}
archivesBaseName = "${mod_name}-quilt-${minecraft_version}"

group 'subaraki.hangman'
version "${version}"

repositories {
    maven {
        name 'Quilt (Release)'
        url 'https://maven.quiltmc.org/repository/release/'
    }

    maven {
        name 'Quilt (Snapshot)'
        url 'https://maven.quiltmc.org/repository/snapshot/'
        content {
            includeGroup 'org.quiltmc.fabric_api_qsl'
            includeGroupByRegex 'org\\.quiltmc\\.qsl(?:\\.[a-z_]+)?'
        }
    }
}
loom {
    runs {
        client {
            client()
            setConfigName("Quilt Client")
            ideConfigGenerated(true)
            runDir("run")
        }
        server {
            server()
            setConfigName("Quilt Server")
            ideConfigGenerated(true)
            runDir("run")
        }
    }
}
dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.officialMojangMappings()
    modImplementation "org.quiltmc:quilt-loader:${quilt_loader_version}"

    modImplementation("org.quiltmc.quilted-fabric-api:quilted-fabric-api:${quilt_qfapi_version}") {
        exclude group: "net.fabricmc"
    }

    implementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.1'
    implementation project(":Common")
    implementation project(":Fabric")
}

processResources {

    from project(":Common").sourceSets.main.resources
    from project(":Fabric").sourceSets.main.resources

    inputs.property "version", project.version

    filesMatching("quilt.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile) {

    source(project(":Common").sourceSets.main.allSource)
    source(project(":Fabric").sourceSets.main.allSource)

}

jar {
    from("LICENSE") {
        rename { "${it}_${mod_name}"}
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            from components.java
        }
    }

    repositories {
        maven {
            url "file://" + System.getenv("local_maven")
        }
    }
}

task publishCurseForge(type: TaskPublishCurseForge) {

    //TODO Make the api token not crash gradle if it's not present
    apiToken = "${api_token_paintings}"

    // The main file to upload
    def mainFile = upload(532595, jar)
    mainFile.releaseType = 'release'
    mainFile.changelog = "${changelog}"
    mainFile.addModLoader('Quilt')
    mainFile.changelogType = 'markdown'
    mainFile.addRequirement('fabric-api')
}